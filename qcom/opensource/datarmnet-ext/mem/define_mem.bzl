load("//build/bazel_common_rules/dist:dist.bzl", "copy_to_dist_dir")
load("//build/kernel/kleaf:kernel.bzl", "ddk_module")

def define_mem(target, variant):
    kernel_build_variant = "{}_{}".format(target, variant)
    include_base = "../../../{}".format(native.package_name())

    target_copts = []

    # Enable C define only for selected target
    if target == "malabar":
        target_copts.append("-DRMNET_LOWMEM_TARGET")

    deps_mem = select({
        "//build/qcom_build_extensions:qtisocrepo_true": ["//soc-repo:all_headers"],
        "//build/qcom_build_extensions:qtisocrepo_false": ["//msm-kernel:all_headers"],
    })

    kernel_build = select({
        "//build/qcom_build_extensions:qtisocrepo_true": "//soc-repo:{}_base_kernel".format(kernel_build_variant),
        "//build/qcom_build_extensions:qtisocrepo_false": "//msm-kernel:{}".format(kernel_build_variant),
    })

    ddk_module(
        name = "{}_rmnet_mem".format(kernel_build_variant),
        out = "rmnet_mem.ko",
        includes = [".", "include/uapi/linux"],
        hdrs = [ "rmnet_mem.h" ],
        srcs = [
            "rmnet_mem_main.c",
            "rmnet_mem_nl.c",
            "rmnet_mem_nl.h",
            "rmnet_mem_pool.c",
            "rmnet_mem_priv.h",
         ],
        kernel_build = kernel_build,
        deps = deps_mem + [":rmnet_mem_uapi_headers"],
        copts = ["-Wno-misleading-indentation"] + target_copts,
    )

    copy_to_dist_dir(
        name = "{}_datarmnet-ext_dist".format(kernel_build_variant),
        data = [
            ":{}_rmnet_mem".format(kernel_build_variant),
        ],
        dist_dir = "out/target/product/{}/dlkm/lib/modules/".format(target),
        flat = True,
        wipe_dist_dir = False,
        allow_duplicate_filenames = False,
        mode_overrides = {"**/*": "644"},
    )
